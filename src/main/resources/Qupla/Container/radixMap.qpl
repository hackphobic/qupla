// implements a simple key/value storage
// needs to be wrapped in an entity function and accessed through
// sending effects to work correctly

template radixMap<K, V>
{
  type Traversal {
    Trit cmd
    K key
    V value
  }

  func V radixMap<K, V> (Trit cmd, K key, V value) {
    param = Traversal {
      cmd = cmd
      key = key
      value = value
    }
    return radixMapTraverse<K, V>(param)
  }

  func V radixMapTraverse<K, V> (Traversal param) {
    // note that for readability we should probably split this function
    // into two functions, one for the case where we call leaf() and one
    // for the case where we do the traverse() and determine nextParam,
    // but that would effectively double the length of the recursive call
    // path, so for storage efficiency reasons we keep them together as one

    keyZero = isZero[sign<K>(param.key)]

    keyTrit = param.key[0]
    return keyZero ? radixMapLeaf<K, V>(param) :
           isOne [keyTrit] ? radixMapTraverse<K, V>(radixMapTraverseNext<K, V>(keyZero, param)) :
           isZero[keyTrit] ? radixMapTraverse<K, V>(radixMapTraverseNext<K, V>(keyZero, param)) :
           isMin [keyTrit] ? radixMapTraverse<K, V>(radixMapTraverseNext<K, V>(keyZero, param)) : null
  }

  func Traversal radixMapTraverseNext<K, V> (Bool keyZero, Traversal param) {
    return not[keyZero] ? Traversal {
      cmd = param.cmd
      key = lshift<K>(param.key)
      value = param.value
    } : null
  }

  func V radixMapLeaf<K, V> (Traversal param) {
    state V value

    oldValue = value

    cmd = param.cmd
    value = isZero[cmd] ? oldValue : isOne[cmd] ? param.value : isMin[cmd] ? 0 : null

    return oldValue
  }
}
