// implements a simple reference counted key/value storage
// needs to be wrapped in an entity function and accessed through
// sending effects to work correctly

template arcRadixMap<K, V>
{
  type Traversal {
    Trit cmd
    K key
    V value
  }

  type Arc {
    V value
    Int count
  }

  func V arcRadixMap<K, V> (Trit cmd, K key, V value) {
    param = Traversal {
      cmd = cmd
      key = key
      value = value
    }
    return arcRadixTraverse<K, V>(param)
  }

  func V arcRadixTraverse<K, V> (Traversal param) {
    // note that for readability we should probably split this function
    // into two functions, one for the case where we call leaf() and one
    // for the case where we do the traverse() and determine nextParam,
    // but that would effectively double the length of the recursive call
    // path, so for storage efficiency reasons we keep them together as one

    keyZero = isZero[sign<K>(param.key)]

    keyTrit = param.key[0]
    return keyZero ? arcRadixLeaf<K, V>(param) :
           isOne [keyTrit] ? arcRadixTraverse<K, V>(arcRadixTraverseNext<K, V>(keyZero, param)) :
           isZero[keyTrit] ? arcRadixTraverse<K, V>(arcRadixTraverseNext<K, V>(keyZero, param)) :
           isMin [keyTrit] ? arcRadixTraverse<K, V>(arcRadixTraverseNext<K, V>(keyZero, param)) : null
  }

  func Traversal arcRadixTraverseNext<K, V> (Bool keyZero, Traversal param) {
    return not[keyZero] ? Traversal {
      cmd = param.cmd
      key = lshift<K>(param.key)
      value = param.value
    } : null
  }

  func V arcRadixLeaf<K, V> (Traversal param) {
    state Arc arc

    oldValue = arc

    cmd = param.cmd
    arc = isZero[cmd] ? oldValue :
          isOne [cmd] ? arcRadixLeafSet<K, V>(oldValue, param.value) :
          isMin [cmd] ? arcRadixLeafRemove<K, V>(oldValue) : null

    return oldValue.value
  }

  func Arc arcRadixLeafSet<K, V> (Arc arc, V value) {
    return Arc {
      value = value
      count = incr<Int>(arc.count)
    }
  }

  func Arc arcRadixLeafRemove<K, V> (Arc arc) {
    count = decr<Int>(arc.count)
    sign = sign<Int>(count)
    return isZero[sign] ? 0 :
           isPositive[sign] ? Arc {
             value = arc.value
             count = count
           } : null
  }
}
